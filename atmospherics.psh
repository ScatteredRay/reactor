// Copyright (c) 2013, Nicholas "Indy" Ray. All rights reserved.
// See the LICENSE file for usage, modification, and distribution terms.

uniform sampler2D color_buffer;
uniform sampler2D depth_buffer;

uniform vec3 light_source;
uniform vec3 sun_color;
uniform float sun_power;

// oclusion sampling
uniform float num_samples;
uniform float weight;
uniform float decay;

// Ambient light in the air, this is wehre far objects get scattering color without direct sun.
// Not ideal since it still takes the angle vs the sun into account.
uniform float ambient;

uniform float extinction;
uniform float angular;

uniform float out_scattering;

// scattering

uniform float rayleigh;
uniform float mie;
uniform float mie_eccentricity;

// Most of the scattering computations are based off of the GDC 02 [HoffmanPreetham] slides.

void main(void)
{

    vec3 sun_radiance = sun_color * sun_power;

    float pi = 3.1415926535897932384626433832795;
    float e = 2.7182818284590452353602874713526;

    float fov = pi/2;

    float exposure = 0.9; // Can we factor this into tonemapping?

    // Rayleigh, proportional to the (wavelength) ^ -4
    // {650nm, 510nm, 475nm} ^ -4
    vec3 rayleigh_wavelengths = vec3(
      0.222178247742313448857047425833908458777743431470498679595485,
      0.586238396110497531116049945267910322541703689423724512021765,
      0.779077254934288645766208857113405758537903195491613819774453);

    float media_depth = 50; // Based off of depth.
    // This wrongly assumes a square view. And also assumes that the TexCoords go from {0, 0} to {1, 1}
    vec2 delta = light_source.xy - gl_TexCoord[0].xy;
    float sun_angle = length(delta) * fov;

    float rayleigh_scattering = (3/(16*pi))*(1 + pow(cos(sun_angle), 2));
    float mie_scattering = pow(1 - mie_eccentricity, 2)/(4*pi*pow(1 + pow(mie_eccentricity, 2) - 2 * mie_eccentricity * cos(sun_angle), 3/2));
    vec3 angular_scattering = angular * rayleigh * rayleigh_scattering * rayleigh_wavelengths + mie * mie_scattering;

    vec4 diffuse = texture2D(color_buffer, gl_TexCoord[0].xy);
    float depth = texture2D(depth_buffer, gl_TexCoord[0].xy).x;

    media_depth = 20 + media_depth * depth;

    vec2 step = delta/num_samples;

    float occlusion = 0.0;

    for(float i=0.0; i<num_samples; i++)
    {
      vec4 sample = texture2D(color_buffer, gl_TexCoord[0].xy + step * i);
      //diffuse.rgb += sample.rgb * (1.0 - sample.a) * weight * pow(decay, i);
      // Perhaps we should use a model that has a decay?
      occlusion += sample.a / num_samples;
    }

    occlusion = ambient + occlusion * (1.0 - ambient);

    // Occlusion being zero would imply no light, very much a night sky, this is likely not desired in a shadowed environment.

    // L0 is diffuse?
    float u = pow(e, -extinction * media_depth);
    vec3 color = diffuse.rgb * ((1.0 - out_scattering) + out_scattering * u) + (1.0/extinction) * sun_radiance * (1.0 - occlusion) * angular_scattering * (1.0 - u);

    gl_FragColor = vec4(color * exposure, 1.0);
}
